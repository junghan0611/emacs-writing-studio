#+title:        Appendix: The Annotated Emacs Writing Studio Configuration
#+property:     header-args:elisp :tangle ../../init.el :results none :eval no
#+startup:      content
#+bibliography: ../library/emacs-writing-studio.bib

I learned writing computer code by typing long listings of Atari BASIC code and slowly but surely understood its syntax and logic. This method is also a great starting point to learn Emacs Lisp. The purpose of this configuration is for you as a starting point. Feel free to modify anything in the {{{ews}}} (EWS) initialisation file and study its effects.

Many Emacs users share their configuration so that you can copy parts of their code as an active learning experience. The copy-paste method is a shortcut to learn more about Emacs Lisp, as you can change the settings and explore the results of your changes.

This appendix presents and explains the /Emacs Writing Studio/ configuration. This configuration stays as close to vanilla GNU Emacs as is humanly bearable for an author. The configuration is annotated to explain the logic of he code and provides with some options for enhancements or additional functionality. This configuration follows the following principles:

- Leverage functionality of the latest version of GNU Emacs
- Use standard keyboard shortcuts
- Centred around Org mode
- No configuration for writing code

#+begin_src elisp :exports none
  ;;; init.el --- Emacs Writing Studio init -*- lexical-binding: t; -*-

  ;; Copyright (C) 2024 Peter Prevos

  ;; Author: Peter Prevos <peter@prevos.net>
  ;; Maintainer: Peter Prevos <peter@prevos.net>

  ;; This file is NOT part of GNU Emacs.
  ;;
  ;; This program is free software; you can redistribute it and/or modify
  ;; it under the terms of the GNU General Public License as published by
  ;; the Free Software Foundation, either version 3 of the License, or
  ;; (at your option) any later version.
  ;;
  ;; This program is distributed in the hope that it will be useful,
  ;; but WITHOUT ANY WARRANTY; without even the implied warranty of
  ;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
  ;; GNU General Public License for more details.
  ;;
  ;; You should have received a copy of the GNU General Public License
  ;; along with this program. If not, see <https://www.gnu.org/licenses/>.
  ;;
  ;;; Commentary:
  ;;
  ;; Emacs Writing Studio init file
  ;; https://lucidmanager.org/tags/emacs
  ;;
  ;; This init file is tangled from the Org mode source:
  ;; documents/ews-book/99-appendix.org
  ;;
  ;;; Code:

#+end_src

* Using EWS
The first part of the configuration sets the basic principles of the EWS configuration, such as package management, the user interface look and feel, the minibuffer completion system and basic settings to enable writing for humans. Chapter [[#chap:ews]] describes using this configuration.

** Basic Configuration
The first part of the configuration checks if the latest version of Emacs is running. EWS leverages some of the latest functionality, so you will need to install this version. Note that the expression ~(< emacs-major-version 29)~ is in Polish notation. In Lisp, the operator is placed before the operands, unlike the more common infix notation which places the operator between the operands, e.g. ~emacs-major-version < 29~. 

#+begin_src elisp :exports none
    ;; Emacs 29? EWS leverages functionality from the latest Emacs version.
#+end_src

#+begin_src elisp
  (when (< emacs-major-version 29)
    (error "Emacs Writing Studio requires Emacs version 29 or later"))
#+end_src

Any changes made by the Emacs customisation system in EWS are stored in =custom.el= instead of directly to the init file. This approach prevents the customisation system modifying the initialisation file. The =custom.el= file is loaded when found. If variables are set in both the custom file and the init file, the latter takes preference.

#+begin_src elisp :exports none
  ;; Custom settings in a separate file and load the custom settings
#+end_src
  
#+begin_src elisp
  (setq-default custom-file (expand-file-name "custom.el" user-emacs-directory))

  (when (file-exists-p custom-file)
    (load custom-file))
#+end_src

** Emacs Packages
This configuration heavily relies on John Wiegley's Use-Package. This package makes it easier to install and configure packages with a standardised and easy to read method. Software developers call such a tool 'syntactic sugar', which is syntax designed to make code easier to read or write, making the language "sweeter" for humans [cite:@landin_1964].

Emacs users have developed and shared thousands of packages with the rest of the community. The first part of the configuration sets the basic elements to load and install packages from the MELPA archive, in addition to the default ELPA repository.

#+begin_src elisp :exports none
  ;; Set package archives
#+end_src

#+begin_src elisp
  (use-package package
    :config
    (add-to-list 'package-archives
                 '("melpa" . "https://melpa.org/packages/"))
    (package-initialize))
#+end_src

#+begin_src elisp :exports none
  ;; Package Management
#+end_src

#+begin_src elisp
  (use-package use-package
    :custom
    (use-package-always-ensure t)
    (package-native-compile t)
    (warning-minimum-level :emergency))
#+end_src

The Use-Package system consists of a set of statements between parenthesis, which in this case is a macro. In it's simplest form it is something like ~(use-package <package-name>)~. The code can also contain one or more sections to set various options. The =:config= section in the first part evaluates code after the package is loaded, which in this case adds MELPA to the archives and initialises the package manager. The =:custom= section in the second Use-Package macro sets three variables, documented in the comments.

The easiest method to install packages is with the Use-Package macro, which downloads, installs and configures a package. The example below downloads and installs the Dracula theme, a popular colour theme available for hundreds of software packages. The first line identifies the package. The ~:ensure t~ in the second line ensures that Emacs downloads the package if not already available. Any code under ~:config~ will run when the package is activated. In this case, we load the theme without asking for confirmation.

#+begin_src elisp :tangle no
  (use-package dracula-theme
    :ensure t
    :config
    (load-theme 'dracula :no-confirm))
#+end_src

These are the basic principles of installing and configuring packages. The Use-Package methodology has many more options, which are discussed in the Appendix.

To read the finer details of the Use-Package macro, read the manual with =C-h R use-package=.

** Emacs Writing Studio Functionality
EWS also provides a range of bespoke convenience functions for various aspects of the writing process. This package is not available on any Emacs repository, but is loaded directly from the GitHub repository.

#+begin_src elisp
  ;; Load EWS functions
#+end_src

#+begin_src elisp
  (load-file (concat (file-name-as-directory user-emacs-directory) "ews.el"))
#+end_src

The ~ews-missing-executables~ function checks if external software is available on your system. Emacs writes a message in the minibuffer if any of the recommended tools is missing. You can jump to the =*Messages*= buffer with =C-h e= to review the output. If packages are missing, then Emacs will function normally, but some features might be unavailable. The relevant chapters in this book provide more details which software is required and the tasks it undertakes.

The input for this function is a list, which is a series of strings between parenthesis that starts with a tick symbol. This symbol prevents Emacs confusing the list of data with a function. In this example, the list also contains other lists.

This function looks whether all these packages are available on your system. Any software in a nested list, such as =("convert" "gm")=, only one of them has to be available as these programs are alternatives for the same functionality.

#+begin_src elisp :exports none
  ;; Check for missing external software
  ;;
  ;; - soffice (LibreOffice): View and create office documents
  ;; - zip: Unpack ePub documents
  ;; - pdftotext (poppler-utils): Convert PDF to text
  ;; - djvu (DjVuLibre): View DjVu files
  ;; - curl: Reading RSS feeds
  ;; - divpng: Part of LaTeX
  ;; - dot (GraphViz): Create note network diagrams
  ;; - convert (ImageMagick): Convert image files 
  ;; - gm (GraphicsMagick): Convert image files
  ;; - latex (TexLive, MacTex or MikTeX): Preview LaTex and export Org to PDF
  ;; - hunspell: Spellcheck. Also requires a hunspell dictionary
  ;; - grep: Search inside files
  ;; - ripgrep: Faster alternative for grep
  ;; - gs (GhostScript): View PDF files
  ;; - mutool (MuPDF): View PDF files
  ;; - mpg321, ogg123 (vorbis-tools), mplayer, mpv, vlc: Media players
  ;; - git: Version control
#+end_src

#+begin_src elisp
  (ews-missing-executables
   '("soffice" "zip" "pdftotext" "ddjvu"
     "curl"
     "dvipng"
     "dot"
     ("convert" "gm")
     "latex"
     "hunspell"
     ("grep" "ripgrep")
     ("gs" "mutool")
     ("mpg321" "ogg123" "mplayer" "mpv" "vlc")
     "git"))
#+end_src

** Look and Feel
The basic idea is to create a clean and keyboard-centric writing interface with minimal distractions.

The first three lines of code for the EWS look and feel disable the toolbar, the menu bar and the scroll bar. The menu bar can be useful for beginners and you can still access it with the =F10= key. If you like to keep the tool, menu and/or scroll bars, then either remove the relevant lines, change the =-1= to a =1= or add two semi colons at the start of the line to convert them to comments.

Vanilla Emacs has the slightly paternalistic habit to require only a single =Y= or =N= answer while on some occasions it requires you to type =yes= or =no=, due to the perceived higher risk of typing the wrong answer. The ~setq~ function sets the ~use-short-answers~ variable to =t=. If you like to retain this behaviour, then change the =t= into a nil. In Emacs Lisp, =t= means TRUE and =nil= is equivalent to FALSE. Confusingly Emacs documentation often mentions to set a value to "non-nil", which is a double negative to suggest setting the variable to true.

#+begin_src elisp :exports none
  ;;; LOOK AND FEEL
  ;; Keyboard-centric user interface removing tool, menu and scroll bars
#+end_src

#+begin_src elisp
  (tool-bar-mode -1)
  (menu-bar-mode -1)
  (scroll-bar-mode -1)
#+end_src

#+begin_src elisp :exports none
  ;; Short answers only please
#+end_src

#+begin_src elisp
  (setq use-short-answers t)
#+end_src

The next two sections of code further improve the Emacs interface with two packages by Emacs guru Protesilaos Stavrou from Cyprus. The spacious padding package creates some whitespace around windows, preventing crammed text on your screen. The Modus Themes package provides a collection of light and dark themes. These themes conform with the highest standard for colour contrast between background and foreground values (WCAG AAA). They also are optimised for users with red-green colour deficiency.

The Spacious Padding package is used using only default values. The =:init= section contains code that Emacs evaluates when loading the package. In this case, it enables the Spacious Padding mode. The =:custom= section also sets the line spacing to a more spacious value. You can read the manual for this mode with =C-h R spacious=.

#+begin_src elisp :exports none
  ;; Spacious padding
#+end_src

#+begin_src elisp
  (use-package spacious-padding
    :custom
    (line-spacing 3)
    :init
    (spacious-padding-mode 1))
#+end_src

The Modus themes package is highly configurable. This Use-Package declaration contains a few sections. The custom section customises variables used in the package. In this case we instruct the package to use italic and bold fonts for emphasis and allow for fonts with fixed and variable pitch. The code also slightly increases the size of headings. You can toggle between a dark and a light version of this theme and the last variable defines which these to toggle between. EWS uses the tinted version of the themes, which you can modify.

The =:custom= section of the macro sets some variables to define fonts. This section also defines which themes are toggled when switching between light and dark themes. The default is the tinted versions. If you would like your configuration to default to the high-contrast versions or one of the two colour blindness-safe versions, customise the ~modus-themes-to-toggle~ variable. To see the possible options for the Modus themes use the help file: =C-h v modus-themes-collection=. Read the package manual for details with =C-h R modus=.

The =:init= section activates the tinted version of the Modus-Vivendi (light) theme. The next section binds some keys to commands to either toggle between dark and light or select any of the available modus themes. All EWS custom keybindings start with =C-c w= as the prefix key and =C-c w t= as the prefix key for the two Modus theme functions. You can obviously change these  The last section hooks the Variable Pitch mode to any buffer in text mode. This means that written prose is displayed in variable pitch, while metadata, code and other items are in fixed pitch. A hook is a construction in Emacs that associates modes with each other. In this case, variable pitch text will is enabled for all text mode buffers.

#+begin_src elisp :exports none
  ;; Modus Themes
#+end_src

#+begin_src elisp
  (use-package modus-themes
    :custom
    (modus-themes-italic-constructs t)
    (modus-themes-bold-constructs t)
    (modus-themes-mixed-fonts t)
    (modus-themes-to-toggle
     '(modus-operandi-tinted modus-vivendi-tinted))
    :init
    (load-theme 'modus-operandi-tinted :no-confirm)
    :bind
    (("C-c w t t" . modus-themes-toggle)
     ("C-c w t m" . modus-themes-select)
     ("C-c w t s" . consult-theme)))

  (use-package mixed-pitch
    :hook
    (text-mode . mixed-pitch-mode))
#+end_src

This last code snippet in the look-and-feel section changes the way Emacs automatically split windows to favour vertical splits over horizontal ones to improve readability. This section also installs the Balanced Windows package which manages window sizes automatically. For example, when opening three windows and you close one, the remaining windows each get half the screen.

#+begin_src elisp :exports none
  ;; Window management
  ;; Split windows sensibly
#+end_src

#+begin_src elisp
  (setq split-width-threshold 120
        split-height-threshold nil)
#+end_src
#+begin_src elisp  :exports none
  ;; Keep window sizes balanced
#+end_src
#+begin_src elisp
  (use-package balanced-windows
    :config
    (balanced-windows-mode))
#+end_src

Alternatively, you can add these settings directly to your =init.el= file by adding the following three lines, with your fonts and sizes of choice. Any settings you ...

#+begin_src elisp :tangle no
  (set-face-attribute 'default nil :font "DejaVu Sans Mono" :height 200)
  (set-face-attribute 'fixed-pitch nil :font "DejaVu Sans Mono" :heigh 200)
  (set-face-attribute 'variable-pitch nil :font "DejaVu Sans")
#+end_src

** Minibuffer Completion
{{{ews}}} uses the Vertico-Orderless-Marginalia stack of minibuffer completion packages in their standard configuration.

#+begin_src elisp :exports none
  ;; MINIBUFFER COMPLETION

  ;; Enable vertico
  #+end_src
  
  #+begin_src elisp
  (use-package vertico
    :init
    (vertico-mode)
    :custom
    (vertico-sort-function 'vertico-sort-history-alpha))
  #+end_src
  
  #+begin_src elisp :exports none
  ;; Persist history over Emacs restarts.
  #+end_src
  
  #+begin_src elisp
  (use-package savehist
    :init
    (savehist-mode))
  #+end_src
  
  #+begin_src elisp :exports none
  ;; Search for partial matches in any order
  #+end_src
  
  #+begin_src elisp
  (use-package orderless
    :custom
    (completion-styles '(orderless basic))
    (completion-category-defaults nil)
    (completion-category-overrides
     '((file (styles partial-completion)))))
  #+end_src
  
  #+begin_src elisp :exports none
  ;; Enable richer annotations using the Marginalia package
  #+end_src
  
  #+begin_src elisp
  (use-package marginalia
    :init
    (marginalia-mode))
#+end_src

** Keyboard Shortcuts Menu
The Which Key package improves discoverability of keyboard shortcuts with a popup in the minibuffer. The columns are widened a bit to prevent truncated function names. Due to the naming conventions in Emacs, most functions start with the package name, so some can be quite long. The problem is that the most interesting part of a function name is at the end of the string so we don't want that to be hidden.

This configuration also instructs Which Key to order the list by function name, rather than by key. In most cases when using this menu, I am looking for a particular function.

#+begin_src elisp :exports none
  ;; Improve keyboard shortcut discoverability
#+end_src
  
#+begin_src elisp
    (use-package which-key
      :config
      (which-key-mode)
      :custom
      (which-key-max-description-length 40)
      (which-key-lighter nil)
      (which-key-sort-order 'which-key-description-order))
#+end_src

** Improved Help Functionality
Emacs is advertised as the self-documenting text editor. While this is not quite correct (if only computer code could document itself), every single aspect of Emacs is documented within the program itself. Firstly there are the manuals and also each function contains some documentation.

The /Helpful/ package by Wilfred Hughes adds contextual information to the built-in Emacs help. When, for example, asking for documentation about a variable, the help file provides links to its customisation screen or the source code.

#+begin_src elisp :exports none
  ;; Improved help buffers
#+end_src
  
#+begin_src elisp
  (use-package helpful
    :bind
    (("C-h x" . helpful-command)
     ("C-h k" . helpful-key)
     ("C-h v" . helpful-variable)))
#+end_src

** Configure Text Modes
Emacs is principally designed for developing computer code, so it needs some modifications to enable writing text for humans. Firstly we hook Visual Line Mode to Text Mode. Visual Line mode wraps long lines to the nearest word to fit in the current window.

By default, Emacs does not replace text when you select a section and then start typing, which is unusual behaviour when writing prose. The =:init= section enables a more common default so that selected text is deleted when typed over. The =:custom= section enables the page-up and page-down keys to scroll all the way to the top or bottom of a buffer. This section also redefines the way Emacs defines a sentence (see section [[#sec:count]]). The last variable saves any existing clipboard text into the kill ring for better operability between the operating system's clipboard and Emacs's kill ring.

#+begin_src elisp :exports none
  ;;; Text mode settings
  #+end_src
  
  #+begin_src elisp
    (use-package text-mode
      :ensure
      nil
      :hook
      (text-mode . visual-line-mode)
      :init
      (delete-selection-mode t)
      :custom
      (sentence-end-double-space nil)
      (scroll-error-top-bottom t)
      (save-interprogram-paste-before-kill t))
#+end_src

** Spellchecking
Writing without automated spell checking would be very hard even for the most experienced authors. The Flyspell package requires the hunspell software to be available and the relevant dictionary. You might want to change the standard dictionary to your local variety with the ~flyspell-default-dictionary~  variable.

#+begin_src elisp :exports none
  ;; Check spelling with flyspell and hunspell
#+end_src
  
#+begin_src elisp
  (use-package flyspell
    :custom
    (flyspell-issue-message-flag nil)
    (ispell-program-name "hunspell")
    (ispell-dictionary "en_AU,nederlands")
    (flyspell-mark-duplications-flag nil) ;; Writegood mode does this
    (org-fold-core-style 'overlays) ;; Fix Org mode bug
    :config
    (ispell-hunspell-add-multi-dic ispell-dictionary)
    (ispell-set-spellchecker-params)
    :hook
    (text-mode . flyspell-mode)
    :bind
    (("C-c w s s" . ispell)
     ("C-;"       . flyspell-auto-correct-previous-word)))
#+end_src

** Ricing Org Mode
This part of the configuration sets a bunch of variables to improve the design of Org mode buffers. Org mode has a lot of other variables you can configure to change its interface. You can easily add other variables or remove some to make Org mode look the way you prefer. For example, to enable alphabetical lists and numerals, you need to customise the ~org-list-allow-alphabetical~ variable to =t=. This adds =a.=, =A.=, =a)= and =A)= as additional options to number a list.

If you have no need for mathematical notation and LaTeX, then you should disable the ~org-startup-with-latex-preview~ variable to prevent error messages and delays in displaying buffers.

#+begin_src elisp :exports none
  ;;; Ricing Org mode
#+end_src
  
#+begin_src elisp
  (use-package org
    :custom
    (org-startup-indented t)
    (org-hide-emphasis-markers t)
    (org-startup-with-inline-images t)
    (org-image-actual-width '(450))
    (org-fold-catch-invisible-edits 'error)
    (org-startup-with-latex-preview t)
    (org-pretty-entities t)
    (org-use-sub-superscripts "{}")
    (org-id-link-to-org-use-id t))
#+end_src

The above code snippet hides emphasis markers from view. Emphasis markers are the symbols used to indicate italics, bold and other font decorations. Hiding the syntax of a plain text document is not a good idea because it obfuscates essential information. The Org Appear package shows hidden markers in Org buffers when the cursor in on a emphasised word. This means that an /italic/ word displays as =/italic/=.

#+begin_src elisp :exports none
  ;; Show hidden emphasis markers
#+end_src
  
#+begin_src elisp  
  (use-package org-appear
    :hook
    (org-mode . org-appear-mode))
#+end_src

The Org Fragtog package is similar to Org Appear, but for LaTeX snippets. It automatically toggles Org mode LaTeX fragment previews as the cursor enters and exits them. By default, the text is a bit small and can become unreadable when changing between dark and light themes. The =org-format-latex-options= variable controls the way the Emacs presents fragments. This variable is a list with properties such as colours and size. The =plist-put= function lets you change one of these options in the list. The foreground and background are set to take the same colour as your text. If you change from dark to light mode or vice versa, you might need to evaluate the ~org-latex-preview~ function (=C-c C-x C-l=) to change the preview images.

#+begin_src elisp :exports none
  ;; LaTeX previews
#+end_src

#+begin_src elisp
  (use-package org-fragtog
    :after org
    :hook
    (org-mode . org-fragtog-mode)
    :custom
    (org-format-latex-options
     (plist-put org-format-latex-options :scale 2)
     (plist-put org-format-latex-options :foreground 'auto)
     (plist-put org-format-latex-options :background 'auto)))
#+end_src

The last package to modify Org buffers is Org Modern. However, most of the features have been switched off because it might be better for beginning users as these settings hide the semantic symbols. You can experiment with changing these settings to change the look and feel of Org mode. 

#+begin_src elisp :exports none
  ;; Org modern: Most features disables for beginnng users
#+end_src

#+begin_src elisp
  (use-package org-modern
    :hook
    (org-mode . org-modern-mode)
    :custom
    (org-modern-table nil)
    (org-modern-keyword nil)
    (org-modern-timestamp nil)
    (org-modern-priority nil)
    (org-modern-checkbox nil)
    (org-modern-tag nil)
    (org-modern-block-name nil)
    (org-modern-keyword nil)
    (org-modern-footnote nil)
    (org-modern-internal-target nil)
    (org-modern-radio-target nil)
    (org-modern-statistics nil)
    (org-modern-progress nil))
#+end_src

* Inspiration
** Read ebooks
The built-in Doc-View package can read various file formats with the assistance of external software. This configuration increases the resolution of the generated image file and raises the threshold for warning before opening large files to fifty MB ($50 \times 2^{20}$). Section [[#sec:pdf]] explains how to use this package.

#+begin_src elisp :exports none
  ;; INSPIRATION

  ;; Doc-View
#+end_src

#+begin_src elisp
  (use-package doc-view
    :custom
    (doc-view-resolution 300)
    (large-file-warning-threshold (* 50 (expt 2 20))))
#+end_src

The Nov package by Vasilij Schneidermann provides useful functionality for viewing ePub books inside Emacs. The init section ensures that any file with an =epub= extension is associated with this package. Refer to section [[#sec:epub]] on how to read ePub files.

#+begin_src elisp :exports none
  ;; Read ePub files
#+end_src

#+begin_src elisp
   (use-package nov
     :init
     (add-to-list 'auto-mode-alist '("\\.epub\\'" . nov-mode)))
#+end_src

There is currently a confirmed bug in Org mode (version 9.6.6) that overrides the associations between LibreOffice and Doc View mode. The code below is a workaround to reinstate the desired behaviour and associates the various file extensions with Doc View. The bug is slotted to be resolved in version 9.7.

#+begin_src elisp :exports none
  ;; Reading LibreOffice files
  ;; Fixing a bug in Org Mode pre 9.7
  ;; Org mode clobbers associations with office documents
#+end_src

#+begin_src elisp
  (use-package ox-odt
    :ensure nil
    :config
    (add-to-list 'auto-mode-alist
                 '("\\.\\(?:OD[CFIGPST]\\|od[cfigpst]\\)\\'"
                   . doc-view-mode-maybe)))
#+end_src

** Bibliographies
These lines of code add two field types to BibTeX entries: keywords to help you order your literature and a link to a file so you can read any attachments in Emacs. The ~ews-register-bibtex~ files assigns the =.bib= files in the ~ews-bibliography-directory~ variable to the list of global BibTeX files. You need to set this variable to the location where you store your bibliography and restart Emacs if needed.

BibTeX mode has many more options that you can configure to modify all sorts of behaviour. This mode is unfortunately not very well documented. 

#+begin_src elisp :exports none
  ;; Managing Bibliographies
#+end_src

#+begin_src elisp
  (use-package bibtex
    :custom
    (bibtex-user-optional-fields
     '(("keywords" "Keywords to describe the entry" "")
       ("file"     "Relative or absolute path to attachments" "" )))
    (bibtex-align-at-equal-sign t)
    :config
    (ews-bibtex-register)
    :bind
    (("C-c w b r" . ews-bibtex-register)))
#+end_src

BibTeX is old but stable software that was last updated in 1988 and has minor limitations. The BibLaTeX dialect is a more recent version that provides more functionality and flexibility. To change BibTeX Mode to BibLaTeX, change the =bibtex-dialect= variable in the configuration to BibLaTeX by adding the following line to your configuration:

#+begin_src elisp :eval no :tangle no
  (bibtex-set-dialect 'biblatex)
#+end_src

The Biblio package provides a useful interface to online literature repositories. The ~ews-biblio-lookup~ function makes this package a little easier to use.

#+begin_src elisp :exports none
  ;; Biblio package for adding BibTeX records
#+end_src

#+begin_src elisp
  (use-package biblio
    :bind
    (("C-c w b b" . ews-bibtex-biblio-lookup)))
#+end_src

#+begin_src elisp :exports none
  ;; Citar to access bibliographies
#+end_src

#+begin_src elisp
  (use-package citar
    :defer t
    :custom
    (citar-bibliography ews-bibtex-files)
    :bind
    (("C-c w b o" . citar-open)))

  (use-package citar-embark
  :after citar embark
  :no-require
  :config (citar-embark-mode)
  :bind (("C-M-." . embark-act)
         :map citar-embark-citation-map
         ("c" . citar-denote-find-citation)))
#+end_src

** Reading Websites
Vanilla Emacs opens hyperlinks to the World Wide Web with your operating system's default browser. If you prefer to use EWW as the default, add this code to your configuration file: ~(setq browse-url-browser-function 'eww-browse-url)~. You can configure the EWW search engine by configuring the ~eww-search-prefix~ variable.

#+begin_src elisp :tangle no
  ;; Use EWW
  ;; (setq browse-url-browser-function 'eww-browse-url)

  ;; Configure Elfeed
#+end_src

#+begin_src elisp :exports none
  ;; Read RSS feeds with Elfeed
#+end_src

#+begin_src elisp
  (use-package elfeed
    :custom
    (elfeed-db-directory
     (expand-file-name "elfeed" user-emacs-directory))
    (elfeed-show-entry-switch 'display-buffer)
    :bind
    ("C-c w e" . elfeed))
#+end_src
  
#+begin_src elisp :exports none
  ;; Configure Elfeed with org mode
#+end_src
  
  #+begin_src elisp
  (use-package elfeed-org
    :config
    (elfeed-org)
    :custom
    (rmh-elfeed-org-files
     (list (concat (file-name-as-directory
                (getenv "HOME"))
                   "Documents/elfeed.org"))))
#+end_src
  
#+begin_src elisp :exports none
  ;; Easy insertion of weblinks
#+end_src
  
#+begin_src elisp
  (use-package org-web-tools
    :bind
    (("C-c w w" . org-web-tools-insert-link-for-url)))
#+end_src

** Playing Multimedia Files
#+begin_src elisp :exports none
  ;; Emacs Multimedia System
#+end_src

#+begin_src elisp
  (use-package emms
    :init
    (require 'emms-setup)
    (require 'emms-mpris)
    (emms-all)
    (emms-default-players)
    (emms-mpris-enable)
    :custom
    (emms-browser-covers #'emms-browser-cache-thumbnail-async)
    :bind
    (("C-c w m b" . emms-browser)
     ("C-c w m e" . emms)
     ("C-c w m p" . emms-play-playlist )
     ("<XF86AudioPrev>" . emms-previous)
     ("<XF86AudioNext>" . emms-next)
     ("<XF86AudioPlay>" . emms-pause)))
#+end_src

** Opening files with external software

#+begin_src elisp
  (use-package openwith
    :config
    (openwith-mode t)
    :custom
    (openwith-association nil))
#+end_src

* Ideation
** Org Capture
You could, for example, create a separate entry for a shopping list. You can access the configuration in the capture menu with the =C= button, which pops up the customisation screen for the ~org-capture-templates~ variable. Next click the =INS= button to add another entry and complete the relevant fields as below and save the new variable. The example below create a shopping list stored in a file in your Dropbox folder. Several mobile apps exist that can read Org mode files, so you can take your list to the shops if you have a means to synchronise the relevant files.

The possibilities for capture templates are extensive and depend on your individual use cases. Explaining the configuration of the Org capture options in detail is outside the scope of this website. The Org manual (=C-h R org ENTER g capture ENTER=) discusses developing capture templates in detail.

#+begin_src elisp
  ;; Fleeting notes
#+end_src

#+begin_src elisp
  (use-package org
    :bind
    (("C-c c" . org-capture)
     ("C-c l" . org-store-link)))
#+end_src
    
#+begin_src elisp
  ;; Capture templates
#+end_src
    
#+begin_src elisp
  (setq org-capture-templates
   '(("f" "Fleeting note"
      item
      (file+headline org-default-notes-file "Notes")
      "- %?")
     ("p" "Permanent note" plain
      (file denote-last-path)
      #'denote-org-capture
      :no-save t
      :immediate-finish nil
      :kill-buffer t
      :jump-to-captured t)
     ("t" "New task" entry
      (file+headline org-default-notes-file "Tasks")
      "* TODO %i%?")))
#+end_src

** Denote
#+begin_src elisp :exports none
  ;; Denote
#+end_src

#+begin_src elisp
  (use-package denote
    :defer t
    :custom
    (denote-sort-keywords t)
    :hook
    (dired-mode . denote-dired-mode)
    :custom-face
    (denote-faces-link ((t (:slant italic))))
    :init
    (require 'denote-org-extras)
    :bind
    (("C-c w d b" . denote-find-backlink)
     ("C-c w d d" . denote-date)
     ("C-c w d f" . denote-find-link)
     ("C-c w d h" . denote-org-extras-link-to-heading)
     ("C-c w d i" . denote-link-or-create)
     ("C-c w d I" . denote-org-extras-dblock-insert-links)
     ("C-c w d k" . denote-keywords-add)
     ("C-c w d K" . denote-keywords-remove)
     ("C-c w d l" . denote-link-find-file)
     ("C-c w d n" . denote)
     ("C-c w d r" . denote-rename-file)
     ("C-c w d R" . denote-rename-file-using-front-matter)))
#+end_src

For the search functionality to work you need to install the RipGrep program, an extremely fast program to search through text files. 

#+begin_src elisp :exports none
  ;; Consult-Denote for easy access
#+end_src

#+begin_src elisp
  (use-package consult-denote
    :custom
    (consult-denote-find-command
     #'(lambda() (find-file (consult-denote-file-prompt))))
    :config
    (consult-denote-mode)
    :bind
    (("C-c w h" . consult-org-heading)
     ("C-c w f" . consult-denote-find)
     ("C-c w g" . consult-denote-grep)
     ("C-x b"   . consult-buffer)))
#+end_src

#+begin_src elisp :exports none
  ;; Citar-Denote to manage literature notes
#+end_src

#+begin_src elisp
  (use-package citar-denote
    :custom
    (citar-open-always-create-notes t)
    :init
    (citar-denote-mode)
    :bind
    (("C-c w b c" . citar-create-note)
     ("C-c w b n" . citar-denote-open-note)
     ("C-c w b x" . citar-denote-nocite)
     :map org-mode-map
     ("C-c w b k" . citar-denote-add-citekey)
     ("C-c w b K" . citar-denote-remove-citekey)
     ("C-c w b d" . citar-denote-dwim)
     ("C-c w b e" . citar-denote-open-reference-entry)))
#+end_src

#+begin_src elisp :exports none
  ;; Explore and manage your Denote collection
#+end_src

#+begin_src elisp
  (use-package denote-explore
    :bind
    (;; Statistics
     ("C-c w x c" . denote-explore-count-notes)
     ("C-c w x C" . denote-explore-count-keywords)
     ("C-c w x b" . denote-explore-keywords-barchart)
     ("C-c w x x" . denote-explore-extensions-barchart)
     ;; Random walks
     ("C-c w x r" . denote-explore-random-note)
     ("C-c w x l" . denote-explore-random-link)
     ("C-c w x k" . denote-explore-random-keyword)
     ;; Denote Janitor
     ("C-c w x d" . denote-explore-identify-duplicate-notes)
     ("C-c w x z" . denote-explore-zero-keywords)
     ("C-c w x s" . denote-explore-single-keywords)
     ("C-c w x o" . denote-explore-sort-keywords)
     ("C-c w x w" . denote-explore-rename-keyword)
     ;; Visualise denote
     ("C-c w x n" . denote-explore-network)
     ("C-c w x v" . denote-explore-network-regenerate)
     ("C-c w x D" . denote-explore-degree-barchart)))
#+end_src

* Production
** Managing the Writing Process
#+begin_src elisp :exports none
  ;; Set some Org mode shortcuts
#+end_src

#+begin_src elisp
  (use-package org
    :bind
    (:map org-mode-map
          ("C-c w n" . ews-org-insert-notes-drawer)
          ("C-c w p" . ews-org-insert-screenshot)
          ("C-c w c" . ews-org-count-words)))
#+end_src

#+begin_src elisp :exports none
  ;; Distraction-free writing
#+end_src

#+begin_src elisp
  (use-package olivetti
    :demand t
    :bind
    (("C-c w o" . ews-olivetti)))
#+end_src

#+begin_src elisp :exports none
  ;; Undo Tree
#+end_src

#+begin_src elisp
  (use-package undo-tree
    :config
    (global-undo-tree-mode)
    :custom
    (undo-tree-auto-save-history nil)
    :bind
    (("C-c w u" . undo-tree-visualize)))
#+end_src

** Citations
#+begin_src elisp :exports none
  ;; Export citations with Org Mode
#+end_src

#+begin_src elisp
  (require 'oc-natbib)
  (require 'oc-csl)

  (setq org-cite-csl-styles-dir ews-bibtex-directory
        org-cite-export-processors
        '((latex natbib "apalike2" "authoryear")
          (t     csl    "apa6.csl"))
        org-cite-global-bibliography ews-bibtex-files
        org-cite-insert-processor 'citar
        org-cite-follow-processor 'citar
        org-cite-activate-processor 'citar)
#+end_src

** Quality Assurance

#+begin_src elisp :exports none
  ;; Lookup words in online dictionary
#+end_src

#+begin_src elisp
  (use-package dictionary
    :custom
    (dictionary-server "dict.org")
    :bind
    (("C-c w s d" . dictionary-lookup-definition)))
#+end_src

The English version of the Synosaurus package depends on the Wordnet software to interface with the online database. 

#+begin_src elisp
  (use-package powerthesaurus
  :bind
  (("C-c w s p" . powerthesaurus-transient)))
#+end_src

The [[https://github.com/bnbeckwith/writegood-mode][writegood package]] helps to detect buzzwords, passive writing and repeated words. This package also contains functions to estimate the complexity of a text.

#+begin_src elisp :exports none
  ;; Writegood-Mode for buzzwords, passive writing and repeated word detection
#+end_src

#+begin_src elisp
  (use-package writegood-mode
    :bind
    (("C-c w s r" . writegood-reading-ease))
    :hook
    (text-mode . writegood-mode))
#+end_src

** Abbreviations
Abbrev mode is a built-in program that helps you speed-up your writing by defining abbreviations and common spelling mistakes and automatically replace these with words, sentences or complete paragraphs.

#+begin_src elisp :exports none
  ;; Abbreviations
#+end_src
  
#+begin_src elisp
  (add-hook 'text-mode-hook 'abbrev-mode)
#+end_src

The Lorem Ipsum generator can be useful when designing the layout of a document. This package inserts dummy Latin text into a buffer. 

#+begin_src elisp :exports none
  ;; Lorem Ipsum generator
#+end_src

#+begin_src elisp
  (use-package lorem-ipsum
    :custom
    (lorem-ipsum-list-bullet "- ")
    :bind
    (("C-c w i s" . lorem-ipsum-insert-sentences)
     ("C-c w i p" . lorem-ipsum-insert-paragraphs)
     ("C-c w i l" . lorem-ipsum-insert-list)))
#+end_src

** Version Control
The ~ediff~ family of function by default does not split its windows nicely, so these settings make the program easier to use.

#+begin_src elisp :exports none
  ;; ediff
#+end_src

#+begin_src elisp
  (use-package ediff
    :ensure nil
    :custom
    (ediff-keep-variants nil)
    (ediff-split-window-function 'split-window-horizontally)
    (ediff-window-setup-function 'ediff-setup-windows-plain))
#+end_src

** Other Text Modes
#+begin_src elisp
  (use-package fountain-mode)
#+end_src

#+begin_src elisp
  (use-package markdown-mode)
#+end_src

* Publication
** Basic Settings

The timestamp for exporting files is set to the European date format of day month and year. If you publish for American audiences, perhaps you like to modify the ~org-export-date-timestamp-format~ to ="%B %e %Y"=. The letters each stand for the full name of the month, the day number without leading zero and the year in four digits. See the documentation for the ~format-time-string~ function for details on how to format dates in other methods.

#+begin_src elisp :exports none
  ;; Generic Org Export Settings
#+end_src

#+begin_src elisp
  (use-package org
    :custom
    (org-export-with-drawers nil)
    (org-export-with-todo-keywords nil)
    (org-export-with-broken-links t)
    (org-export-with-toc nil)
    (org-export-with-smart-quotes t)
    (org-export-date-timestamp-format "%e %B %Y"))
#+end_src

** Office Documents
#+begin_src elisp :tangle no
  ;; Not included in EWS

  ;; Export ODT to MS-Word
  (setq-default org-odt-preferred-output-format "docx")
  
  ;; Export ODT to PDF
  (setq-default org-odt-preferred-output-format "pdf")
#+end_src

** Latex
#+begin_src elisp:exports none
  ;; LaTeX PDF Export settings
#+end_src

#+begin_src elisp
  (use-package ox-latex
    :ensure nil
    :demand t
    :custom
    ;; Multiple LaTeX passes for bibliographies
    (org-latex-pdf-process
     '("pdflatex -interaction nonstopmode -output-directory %o %f"
       "bibtex %b"
       "pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"
       "pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"))
    ;; Clean temporary files after export
    (org-latex-logfiles-extensions
     (quote ("lof" "lot" "tex~" "aux" "idx" "log" "out"
             "toc" "nav" "snm" "vrb" "dvi" "fdb_latexmk"
             "blg" "brf" "fls" "entoc" "ps" "spl" "bbl"
             "tex" "bcf"))))
#+end_src

#+begin_src elisp :exports none
  ;; LaTeX templates
#+end_src

#+begin_src elisp
  (with-eval-after-load 'ox-latex
    (add-to-list
     'org-latex-classes
     '("crc"
       "\\documentclass[krantz2]{krantz}
          \\usepackage{lmodern}
          \\usepackage[authoryear]{natbib}
          \\usepackage{nicefrac}
          \\usepackage[bf,singlelinecheck=off]{caption}
          \\captionsetup[table]{labelsep=space}
          \\captionsetup[figure]{labelsep=space}
          \\usepackage{Alegreya}
          \\usepackage[scale=.8]{sourcecodepro}
          \\usepackage[breaklines=true]{minted}
          \\usepackage{rotating}
          \\usepackage[notbib, nottoc,notlot,notlof]{tocbibind}
          \\usepackage{amsfonts, tikz, tikz-layers}
          \\usetikzlibrary{fadings, quotes, shapes, calc, decorations.markings}
          \\usetikzlibrary{patterns, shadows.blur}
          \\usetikzlibrary{shapes,shapes.geometric,positioning}
          \\usetikzlibrary{arrows, arrows.meta, backgrounds}
          \\usepackage{imakeidx} \\makeindex[intoc]
          \\renewcommand{\\textfraction}{0.05}
          \\renewcommand{\\topfraction}{0.8}
          \\renewcommand{\\bottomfraction}{0.8}
          \\renewcommand{\\floatpagefraction}{0.75}
          \\renewcommand{\\eqref}[1]{(Equation \\ref{#1})}
          \\renewcommand{\\LaTeX}{LaTeX}"
       ("\\chapter{%s}" . "\\chapter*{%s}")
       ("\\section{%s}" . "\\section*{%s}")
       ("\\subsection{%s}" . "\\subsection*{%s}")
       ("\\subsubsection{%s}" . "\\paragraph*{%s}"))))
#+end_src

** ePub
#+begin_src elisp:exports none
  ;; epub export
#+end_src

#+begin_src elisp
  (use-package ox-epub
    :demand t)
#+end_src

** Advanced Export Settings for EWS                               :noexport:
#+begin_src elisp
  ;; ADVANCED NDOCUMENTED EXPORT SETTINGS FOR EWS
  
  ;; Use GraphViz for flow diagrams
  (org-babel-do-load-languages
   'org-babel-load-languages
   '((dot . t))) ; this line activates dot
#+end_src

* Administration

** Getting Things Done
#+begin_src elisp :exports none
  ;;; ADMINISTRATION

  ;; Bind org agenda command
#+end_src

#+begin_src elisp
  (use-package org
    :custom
    (org-log-into-drawer t)
    (org-enforce-todo-checkbox-dependencies t)
    (org-enforce-todo-dependencies t)
    :bind
    (("C-c a" . org-agenda)))
#+end_src

** Manage Files
The dired package is a convenient and powerful tool to keep your drives organised and access your information. Developers have published an extensive collection of extensions to dired to add functionality, which you can find in the package manager.

Dired lists files and directories in alphabetical order. I prefer a different view, which shows directories on top and files below them. The parameters determine the order of the entries in the folder.


#+begin_src elisp :exports none
  ;; FILE MANAGEMENT
#+end_src

#+begin_src elisp
  (use-package dired
    :ensure
    nil
    :commands
    (dired dired-jump)
    :custom
    (dired-listing-switches
     "-goah --group-directories-first --time-style=long-iso")
    (dired-dwim-target t)
    (delete-by-moving-to-trash t)
    :init
    (put 'dired-find-alternate-file 'disabled nil))
#+end_src
  
#+begin_src elisp :exports none
  ;; Hide hidden files
#+end_src
  
#+begin_src elisp
  (use-package dired-hide-dotfiles
    :hook
    (dired-mode . dired-hide-dotfiles-mode)
    :bind
    (:map dired-mode-map ("." . dired-hide-dotfiles-mode)))
#+end_src

This next bit of configuration defines how Emacs manages automated backups. The default setting is that the system stores these files in the folder where the original files lives, cluttering folders with copies of your stuff. The setting below modifies the =backup-directory-alist= variable so that Emacs saves all backups (indicted by ="."=) in the =bak= subdirectory your init folder. Alternatively, you could instruct Emacs not to save backups at all with ~(setq-default make-backup-files nil)~. I prefer keeping backups as they have saved my bacon a few times in the past.

This configuration also eliminates lock files, which are only useful when working in shared folders. This file prevent other users from opening a file when another user is already editing the file. Change this variable to =t= if you collaborate with other through a file-sharing service such as Nextcloud.

#+begin_src elisp :exports none
  ;; Backup files
#+end_src
  
#+begin_src elisp
  (setq-default backup-directory-alist
                `(("." . ,(expand-file-name "backups/" user-emacs-directory)))
                version-control t
                delete-old-versions t
                create-lockfiles nil)
#+end_src

The function to save the recent files runs every five minutes, instead of only when Emacs exists. The function to save the list of recent files needs some modifictaion to prevent messages popping up in the buffer every so often.

#+begin_src elisp :exports none
  ;; Recent files
#+end_src

#+begin_src elisp
  (use-package recentf
    :config
    (recentf-mode t)
    (run-at-time nil (* 5 60)
                 (lambda () (let ((save-silently t))
                              (recentf-save-list))))
    :custom
    (recentf-max-saved-items 50)
    :bind
    (("C-c w r" . recentf-open)))

  #+end_src
  
#+begin_src elisp :exports none
  ;; Bookmarks
#+end_src
  
#+begin_src elisp
  (use-package bookmark
    :custom
    (bookmark-save-flag 1)
    :bind
    ("C-x r D" . bookmark-delete))
#+end_src

* Enhancing Emacs Wring Studio
    Don't add something to your Emacs configuration until you understand exactly what it does

** Modifying Key Sequences
Emacs ships with a range of predefined keyboard shortcuts for its core functionality and the built-in packages. Most external packages don't define key keyboard shortcuts to prevent conflicts with your configuration. The EWS configuration assigns shortcuts to the most common functions.

You can change the keyboard's behaviour at three levels: programmable keyboards, the operating system/window manager, and Emacs.

Some high-end keyboards are programmable and let you define the output of each key. For example, you could map the right control key as the Hyper key. At the second level, your operating system interprets the input from the keyboard. In Windows, =s-E= (Windows and E) opens the file explorer. You can erase this binding to make it available in Emacs. Each operating system has its own methods to change keyboard maps (keymaps). Some experienced Emacs users remap the caps lock key to act as the control key to make it easier to use.

Last but not least, you can define key sequences within Emacs itself. The example below binds =F5= to toggling whitespace mode. This minor mode indicates whitespace in the current buffer with characters. The =#'= characters before the function name are a technical requirement to instruct Emacs not to evaluate this function but only to store its value. If you like to remove a keystroke, just use ~nil~ as the function.

#+begin_src elisp :tangle no
  (keymap-global-set "<F5>" #'whitespace-mode)
#+end_src

This example uses the global keymap, meaning the shortcut is available in all modes. You can also define a shortcut for a specific mode, which is only available when that mode is active. The example below sets the same shortcut but only applies when Org mode is active.

#+begin_src elisp :tangle no
  (keymap-set org-mode-map "C-t" #'whitespace-mode)
#+end_src

The key to good keyboard shortcuts is to encode semantic information, which is why almost all EWS-specific shortcuts start with =C-c w= where the =w= stands for 'writing'. 

Some people don’t like the Emacs keyboard defaults much because they require frequent use of the modifier keys. These people suggest that repetitive use of these keys causes strain injury, the dreaded’ Emacs pinky’. Several packages, such as Evil Mode and God Mode, exist within the Emacs ecosystem that change the default keybindings to a different model. /Emacs Writing Studio/ follows the standard conventions and does not modify default keybindings.

